###############################################################################
# SPDX-License-Identifier: GPL-3.0-only
# PURPOSE: Search content for known bad strings
###############################################################################

sub nikto_content_search_init {
    use vars qw/$CONTENTSEARCH %CSMATCHED/;
    my $id = { name        => "content_search",
               full_name   => "Content Search",
               author      => "Sullo",
               description => "Search response content for interesting strings",
               hooks       => {
                          start => { method => \&nikto_content_search_load,
                                     weight => 1,
                                     },
                          postfetch => { method => \&nikto_content_search,
                                         weight => 20,
                                         },
                            },
               copyright => "2010 Chris Sullo"
               };

    return $id;
}

sub nikto_content_search_load {

    # Load up the database as soon as we can

    $CONTENTSEARCH = init_db("db_content_search");
    %CSMATCHED     = ();

    # to try and speed it up - precompile the regular expressions
    foreach my $testid (@$CONTENTSEARCH) {
        $testid->{'compiled'} = qr/$testid->{'matchstring'}/;
    }
}

sub nikto_content_search {
    my ($mark, $parameters, $request, $response) = @_;

    my $whisker = $response->{'whisker'};
    my $body    = $whisker->{'data'};
    my ($file)  = LW2::uri_split($whisker->{'uri'});
    my $method  = $whisker->{'method'} || "GET";
    my $host    = $mark->{'hostname'};
    my $matched = ($CSMATCHED{$host} ||= {});

    foreach my $testid (@$CONTENTSEARCH) {
        next if $matched->{$file};

        # Check whether we've already matched it
        if ($body =~ $testid->{'compiled'}) {
            my $outmessage = "$file: $testid->{'message'}";
            add_vulnerability($mark, $outmessage, $testid->{'nikto_id'}, $testid->{'refs'},
                              $method, $file, $request, $response);
            $matched->{$file} = 1;
        }
    }
    return $request, $response;
}

1;
