###############################################################################
# SPDX-License-Identifier: GPL-3.0-only
# PURPOSE: Various file operations
###############################################################################
sub nikto_fileops_init {
    my $id = { name        => "fileops",
               full_name   => "File Operations",
               author      => "Sullo",
               description => "Saves results to a text file.",
               copyright   => "2012 Chris Sullo",
               };
    return $id;
}

###############################################################################
# save a result to text file
sub save_item {
    require JSON::PP;
    my ($resulthash, $message, $request, $response) = @_;
    return if $resulthash->{'mark'}->{'save_prefix'} eq '';
    my $fn =
        $resulthash->{'mark'}->{'save_dir'} . "/"
      . $resulthash->{'mark'}->{'save_prefix'}
      . $resulthash->{'nikto_id'} . ".txt";
    my $json_request    = JSON::PP->new->utf8(1)->allow_nonref(1)->encode($request);
    my $json_response   = JSON::PP->new->utf8(1)->allow_nonref(1)->encode($response);
    my $cookies_written = 0;

    open(SAVEFILE, ">>$fn") || die print "ERROR opening savefile '$fn': $@\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    print SAVEFILE "			  Information\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    print SAVEFILE "Test ID:  \t" . $resulthash->{'nikto_id'} . "\n";
    print SAVEFILE "References: \t" . $resulthash->{'refs'} . "\n";
    print SAVEFILE "Message:  \t" . $message . "\n";
    print SAVEFILE "Reason:   \t" . $resulthash->{'reason'} . "\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    print SAVEFILE "			  Request\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";

    if ($request->{'whisker'}{'method'} eq '') {
        print SAVEFILE "Not Applicable\n";
    }
    else {
        my $request_string = rebuild_request($request);
        print SAVEFILE $request_string;
    }
    print SAVEFILE "\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    print SAVEFILE "			  Response\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    if ($response->{'whisker'}{'protocol'} eq '') {
        print SAVEFILE "Not Applicable\n";
    }
    else {
        print SAVEFILE rebuild_response($response);
    }

    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    print SAVEFILE "			  Data Objects\n";
    print SAVEFILE $VARIABLES{'DIV'} . "\n";
    if ($request->{'whisker'}{'method'} ne '') {
        print SAVEFILE "REQUEST:$json_request\n";
    }
    if ($response->{'whisker'}{'protocol'} ne '') {
        print SAVEFILE "RESPONSE:$json_response\n";
    }
    close(SAVEFILE);
}

###############################################################################
# get the prefix for all current host's save files
sub save_getprefix {
    my ($mark) = @_;
    my $prefix = $mark->{'display_name'} . '_' . $mark->{'port'};
    $prefix =~ s/,/\-/g;
    $prefix =~ s/[^a-zA-Z0-9\.\-\_]/_/g;
    $prefix =~ s/_+/_/g;
    my $now = date_disp(time());
    $now =~ s/\s.*$//;
    $prefix .= "_" . $now . "_";
    return $prefix;
}

###############################################################################
# Define/create the save directory
sub save_createdir {
    my ($savedir, $mark) = @_;
    if ($savedir eq '.') {
        $savedir = 'savedir_' . $mark->{'display_name'} . '_' . $mark->{'port'};
        $savedir =~ s/,/\-/g;
        $savedir =~ s/[^a-zA-Z0-9\.\-\_]/_/g;
        $savedir =~ s/_+/_/g;
        my $now = date_disp(time());
        $now =~ s/[^0-9-]/-/g;
        $savedir .= "_" . $now;
    }
    elsif (-f $savedir) {
        nprint("+ ERROR: Directory from -Savedir is a file.");
        exit 1;
    }

    # create?
    if (!-d $savedir) {
        mkdir($savedir);
        if (!-d $savedir) {
            nprint("+ ERROR: Unable to create -Save directory '$savedir'",
                   "", ($mark->{'hostname'}, $mark->{'ip'}, $mark->{'displayname'}));
            exit 1;
        }
    }
    return $savedir;
}

###############################################################################
# Parse input host files
sub parse_hostfile {
    my ($file) = @_;
    my (@results, $hostdesc);
    my $nmap = 0;

    open(IN, $file) || die print STDERR "+ ERROR: Cannot open '$file':$@\n";
    while (<IN>) {
        my $found = 0;

        # Check whether this is a greppable nmap file
        chomp;
        $nmap = 1 if (/^# Nmap/);
        s/\#.*$//;
        if ($_ eq "") { next; }

        # Parse for nmap files
        if ($nmap) {
            if (($_ !~ /Host/) || ($_ !~ /Ports/) || ($_ !~ /open/) || ($_ !~ /(?:http|ssl)/i)) {
                next;
            }

            # parse out the line
            my @fields = split("\t", $_);

            # Get the host name
            $fields[0] =~ /Host:\s+($LW2::IPv4_re|$LW2::IPv6_re_inc_zoneid)\s+\(([^\)]+)?\)/;
            $hostdesc = ($2 ne "") ? $2 : $1;

            # Parse the ports list from:
            # Host: 1.0.0.0 ()  Ports: 80/open/tcp//http///, 8000/open/tcp//http-alt///
            $fields[1] =~ s/^Ports: //;
            my @ports = parse_csv($fields[1]);
            foreach my $nmp (@ports) {
                if (($nmp !~ /(?:80|443)?\/open\/tcp/) || ($nmp !~ /(?:http|ssl)/i)) {
                    nprint("\tNon web port identified: $hostdesc:$nmp", "d", ($hostdesc));
                    next;
                }
                $nmp =~ /^(?:\s+)?(\d+)\//;
                nprint("+ nmap Input Queued: $hostdesc:$1");
                push(@results, $hostdesc . "_" . $1);
            }
        }
        else {
            # just add it to the list
            push(@results, $_);
        }
    }
    close(IN);
    return (@results);
}

1;
